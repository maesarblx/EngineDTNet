#version 330 core
out vec4 FragColor;
  
in vec2 TexCoords;

uniform sampler2D tex;
uniform float saturation;
uniform float brightness;
uniform float exposure;
uniform float gamma;
uniform float contrast;
uniform vec3 tint;

void main()
{
    vec4 texColor = texture(tex, TexCoords);
    vec3 color = texColor.rgb;

    // color = pow(color, vec3(2.2));

    // 1) exposure
    color *= exposure;

    // 2) tone mapping
    color = vec3(1.0) - exp(-color);

    // 3) contrast
    color = ((color - 0.5) * contrast) + 0.5;

    // 4) saturation
    float luminance = dot(color, vec3(0.2126, 0.7152, 0.0722));
    color = mix(vec3(luminance), color, saturation);

    // 5) brightness and tint
    color += vec3(brightness);
    color *= tint;

    // 6) clamp and gamma encode
    color = clamp(color, 0.0, 1.0);
    color = pow(color, vec3(1.0 / gamma));

    FragColor = vec4(color, texColor.a);
}